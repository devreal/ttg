include(AddTTGExecutable)

add_ttg_executable(test test/test.cc)
add_ttg_executable(t9 t9/t9.cc)
add_ttg_executable(t9-streaming t9/t9_streaming.cc)

# sparse matmul need Eigen ... it's always provided by TA
if (TARGET tiledarray)
    # MADworld used for MADNESS serialization
    add_ttg_executable(spmm spmm/spmm.cc LINK_LIBRARIES TiledArray_Eigen)
    # block-sparse needs BTAS ... it's always provided by TA
    # since only need to use matrices, limit BTAS_TARGET_MAX_INDEX_RANK to 2
    add_ttg_executable(bspmm spmm/spmm.cc LINK_LIBRARIES tiledarray TiledArray_Eigen BTAS Boost::boost COMPILE_DEFINITIONS BLOCK_SPARSE_GEMM=1;BTAS_TARGET_MAX_INDEX_RANK=2)

    add_ttg_executable(testing_dpotrf potrf/testing_dpotrf.cc LINK_LIBRARIES tiledarray lapackpp)
    add_ttg_executable(testing_dtrtri potrf/testing_dtrtri.cc LINK_LIBRARIES tiledarray lapackpp)
    add_ttg_executable(testing_dlauum potrf/testing_dlauum.cc LINK_LIBRARIES tiledarray lapackpp)
    add_ttg_executable(testing_dpoinv potrf/testing_dpoinv.cc LINK_LIBRARIES tiledarray lapackpp)

    if (TARGET Kokkos::kokkos)
      add_library(plgsy-device STATIC potrf/core_plgsy.cu)
      target_link_libraries(plgsy-device PRIVATE Kokkos::kokkos)
      target_compile_options(plgsy-device PRIVATE --extended-lambda --expt-relaxed-constexpr -I${CMAKE_CURRENT_SOURCE_DIR}/../ttg/ -I${CMAKE_CURRENT_SOURCE_DIR})
      target_compile_definitions(plgsy-device PRIVATE -DTTG_HAVE_KOKKOS)
      # TODO: target_include_directories does not seem to work with CUDA :/
      include_directories(${CMAKE_CURRENT_SOURCE_DIR}/../ttg/ ${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_CURRENT_SOURCE_DIR}/potrf)
      #add_library(plgsy-host STATIC potrf/core_plgsy.cc)
      #target_link_libraries(plgsy-host PRIVATE Kokkos::kokkos)
    endif(TARGET Kokkos::kokkos)

    if (TARGET CUDA::cublas)
      add_ttg_executable(bspmm-cuda spmm/spmm_cuda.cc
                         LINK_LIBRARIES tiledarray TiledArray_Eigen BTAS Boost::boost CUDA::cublas
                         COMPILE_DEFINITIONS BLOCK_SPARSE_GEMM=1;BTAS_TARGET_MAX_INDEX_RANK=2
                         RUNTIMES "parsec")

      if (TARGET CUDA::cusolver)
        add_ttg_executable(testing_dpotrf_cuda potrf/testing_dpotrf.cc
                          LINK_LIBRARIES lapackpp tiledarray CUDA::cublas CUDA::cusolver
                                          $<$<TARGET_EXISTS:Kokkos::kokkos>:$<TARGET_OBJECTS:Kokkos::kokkoscore>>
                                          $<$<TARGET_EXISTS:Kokkos::kokkos>:plgsy-device>
                          COMPILE_DEFINITIONS TTG_ENABLE_CUDA=1;$<$<TARGET_EXISTS:Kokkos::kokkos>:TTG_HAVE_KOKKOS=1> #;DEBUG_TILES_VALUES=1
                          RUNTIMES "parsec")
      endif(TARGET CUDA::cusolver)
    elseif (TARGET roc::hipblas)
      add_ttg_executable(bspmm-hip spmm/spmm_cuda.cc
                          LINK_LIBRARIES tiledarray TiledArray_Eigen BTAS Boost::boost roc::hipblas
                          COMPILE_DEFINITIONS BLOCK_SPARSE_GEMM=1;BTAS_TARGET_MAX_INDEX_RANK=2
                          RUNTIMES "parsec")
      if (TARGET roc::hipsolver)
        add_ttg_executable(testing_dpotrf_hip potrf/testing_dpotrf.cc
                           LINK_LIBRARIES lapackpp tiledarray roc::hipblas roc::hipsolver
                                          $<$<TARGET_EXISTS:Kokkos::kokkos>:$<TARGET_OBJECTS:Kokkos::kokkoscore>>
                                          $<$<TARGET_EXISTS:Kokkos::kokkos>:plgsy-device>
                           COMPILE_DEFINITIONS TTG_ENABLE_HIP=1;$<$<TARGET_EXISTS:Kokkos::kokkos>:TTG_HAVE_KOKKOS=1> #;DEBUG_TILES_VALUES=1
                           RUNTIMES "parsec")
      endif(TARGET roc::hipsolver)
    elseif (TARGET MKL::MKL_DPCPP)
      add_ttg_executable(bspmm-lz spmm/spmm_cuda.cc
                          LINK_LIBRARIES tiledarray TiledArray_Eigen BTAS Boost::boost MKL::MKL_DPCPP level_zero::ze_loader m
                          COMPILE_DEFINITIONS BLOCK_SPARSE_GEMM=1;BTAS_TARGET_MAX_INDEX_RANK=2
                          RUNTIMES "parsec")
    endif()

    add_ttg_executable(chain-ttg-dev task-benchmarks/chain-ttg-dev.cc LINK_LIBRARIES tiledarray RUNTIMES "parsec")
endif()

if (TARGET MADworld)
  add_ttg_executable(madness-1d madness/madness-1d/madness-1d.cc RUNTIMES "mad")
  if (TARGET blaspp) #(CBLAS_FOUND AND MKL_FOUND)
    add_ttg_executable(mrattg madness/mrattg.cc mragl.cc mratwoscale.cc mradomain.h mrafunctiondata.h mrafunctionfunctor.h mrafunctionnode.h mragl.h mrahash.h mrakey.h mramisc.h mramxm.h mrarange.h mrasimpletensor.h mratwoscale.h mratypes.h LINK_LIBRARIES blaspp MADworld)

    add_ttg_executable(mrattg-streaming madness/mrattg_streaming.cc mragl.cc mratwoscale.cc mradomain.h mrafunctiondata.h mrafunctionfunctor.h mrafunctionnode.h mragl.h mrahash.h mrakey.h mramisc.h mramxm.h mrarange.h mrasimpletensor.h mratwoscale.h mratypes.h LINK_LIBRARIES blaspp MADworld)
  endif ()
endif (TARGET MADworld)

add_ttg_executable(wavefront-wf wavefront/wavefront-wf.cc SINGLERANKONLY)
add_ttg_executable(wavefront-wf2 wavefront/wavefront-wf2.cc SINGLERANKONLY)
add_ttg_executable(wavefront-df wavefront/wavefront-df.cc)
add_ttg_executable(wavefront-pull wavefront/wavefront-pull.cc LINK_LIBRARIES MADworld)
add_ttg_executable(fw-apsp floyd-warshall/floyd_warshall.cc LINK_LIBRARIES MADworld SINGLERANKONLY)
add_ttg_executable(helloworld helloworld/helloworld.cpp)
add_ttg_executable(simplegenerator simplegenerator/simplegenerator.cc RUNTIMES "mad")

add_ttg_executable(device_mock device_mock/device_mock.cc LINK_LIBRARIES MADworld)

if (OpenMP_CXX_FOUND AND TARGET std::execution)
        add_ttg_executable(fw-apsp-df floyd-warshall/floyd_warshall_df.cc LINK_LIBRARIES OpenMP::OpenMP_CXX std::execution MADworld)
endif ()
add_ttg_executable(ge ge/ge.cc SINGLERANKONLY)
if (TARGET std::execution)
        add_ttg_executable(ge-df ge/ge_df.cc LINK_LIBRARIES std::execution MADworld)
endif (TARGET std::execution)
add_ttg_executable(sw sw/sw.cc)

# RandomAccess HPCC Benchmark
if (TARGET MADworld)
  add_ttg_executable(randomaccess randomaccess/randomaccess.cc RUNTIMES "mad")
endif (TARGET MADworld)

